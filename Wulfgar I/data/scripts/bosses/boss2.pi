namespace es_tipolisto_WulfgarI;

/*
    Pantalla 8: Boss 2 pájaro
    Se mueve hacia el player solo en horizontal, 
    cada 0.5 segundos te lanza un buitre que te persigue
    cada 2 segundo lanza 6 aguilas que salen de la derecha.
 */

class Boss2 implements GL_sprite{
	properties:
		incrementoX=0;
		incrementoY=0;
		game=null;
        player1=null;
        boss_live=300;
        boss_dead=false;
        timer_create_enemies=null;
        timer_play_laugh_boss=null;

	virtual _operator_new(_name,_creator, _params){
        game=_params[0];

        Addanimationwithsheet("boss2","images/objects-enemies.png",0,1,0,10*32,32,32,-1,-1,-1,.2);
		Setanimation("boss2");
        SetCollision(True);
        Setcollisionrect(0,10,32*3,32*2);
        SetPos(500,game.GROUND_Y-64);
        SetSize(32*3,32*3, true);

        
        timer_play_laugh_boss=NewObject("timer", "GL_Timer");
        timer_play_laugh_boss.SetFrequency(15000);
        timer_play_laugh_boss.SetUserCallBack(this,"OnTimerLaughBossCallBack");

        //Creamos un buffer de 10 pájaros aleatoriamente, importante la velocidad a 0 que la vamos a necesitar
        for(i=0;i<10;i++){
            //Fijate que los pájaros son del 1 al 4 (mira los defines del main.pi)
            _type_random=rand(1,4);
            //Lo ponemos fuera de la pantalla
            _enemy=game.create_enemy(_type_random,-100,-100);
            _enemy.speedX=0;
        }
        // Creamos un buffer de 3 buitres para ir disparando
        for(i=0;i<3;i++){
            //Lo ponemos fuera de la pantalla
            _enemy=game.create_enemy(ENEMY_TYPE_VULTURE,-100,-100);
            _enemy.speedX=0;
        }

        //cada 10 segundos tira 4 águilas qpor la derecha
        timer_create_enemies=game.NewObject("timer", "GL_Timer");
        timer_create_enemies.SetFrequency(10000);
        timer_create_enemies.SetUserCallBack(this,"OnTimerEnemiesCallBack");
    }

	virtual Move(){   
        if(boss_dead){
            if(GetAnimationLoops()>0) {
                SetAnimationSpeedFactor(0);
                SetColor(RGB(255,0,0));
                SetCollision(false);
                game.changeStage();
            }
        }else{
            if(boss_live<=0){
                //SetAnimation("buitre-die");
                boss_dead=true;
            }else{
                //Se moverá hacia el player
                if(GetX()>=game.player1.GetX()){
                    incrementoX=-1;
                }else if(GetX()<=game.player1.GetX()){
                    incrementoX=1;
                }
                    
                if(GetY()>=game.player1.GetY()){
                    incrementoY=-1;
                }else if(GetY()<=game.player1.GetY()){
                    incrementoY=1;
                }

                //Sacamos del array de enemigos un buitre cada 20 animaciones
                if(GetAnimationLoops()>20){
                    for(i=0;i<sizeof(game.enemies);i++){
                        _enemy=game.enemies[i];
                        if(_enemy.GetItemType()==ENEMY_TYPE_VULTURE){
                            if(_enemy.speedX==0){
                                _enemy.SetPos(GetX(),GetY());
                                //Le ponemos una velocidad
                                if(game.player1.GetX()<GetX()){
                                    _enemy.speedX=-5;
                                    _enemy.SetHFlip(true);
                                }else{
                                    _enemy.speedX=5;
                                    _enemy.SetHFlip(false);
                                }
                                break;
                            }
                        }
                    }
                    ResetAnimation ();
                }          
                incPos(incrementoX, incrementoY);
            }
        }
	}

    function OnTimerEnemiesCallBack(_timer){
        _count=0;
       //Escogemos 4 pajaros que encontremos de la lista de enemigos de tipo pájaro y le ponemos la velocidad a -5
        for(i=0;i<sizeof(game.enemies);i++){
            _enemy=game.enemies[i];
            if(_enemy.GetItemType()==ENEMY_TYPE_EAGLE || 
               _enemy.GetItemType()==ENEMY_TYPE_RAVEN || 
               _enemy.GetItemType()==ENEMY_TYPE_EAGLE_WHITE || 
               _enemy.GetItemType()==ENEMY_TYPE_RAVEN_BLACK){
                if(_enemy.speedX==0){
                    //lo colocamos a la derecha de la pantalla
                    //_x_right_screen=game.player1.GetX()+350;
                    _x_right_screen=VIEWPORT_WIDTH-64;
                    _y_random=rand(100,400);
                    _enemy.SetPos(_x_right_screen,_y_random);
                    //Le ponemos una velocidad
                    _enemy.speedX=-5;
                    _count++;
                    if(_count>5)break;
                }
            }
        }
    }

    virtual _operator_delete(){
        delete timer_create_enemies;
        delete timer_play_laugh_boss;
    }

    
    function OnTimerLaughBossCallBack(_timer){
        //GetApp().GetEngine().LogPrint("timer play laugh boss");
        GetApp().StageGlobal.play_sound_laugh_boss();
    }
}
